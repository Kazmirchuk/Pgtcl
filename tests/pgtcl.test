if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import -force ::tcltest::*
}

foreach F [list ../libpgtcl1.5.so ./libpgtcl1.5.so] {

    if {[file exists $F]} {

        load $F
    }
}

#
#   set this to your specific location
#
array set conninfo {
    host    192.168.123.180
    port    5741
    dbname  template1
    user    postgres
}

#
#
#
test pgtcl-1.1 {connect to db using old connection string} {

    set conn [pg_connect $::conninfo(dbname) \
        -host $::conninfo(host) \
        -port $::conninfo(port) \
        -user $::conninfo(user)]

    pg_disconnect $conn

    set conn
} pgsql3


#
#
#
test pgtcl-1.2 {connect to db using conninfo string} {

    set conn [pg_connect -conninfo \
        "dbname=$::conninfo(dbname) host=$::conninfo(host) port=$::conninfo(port) user=$::conninfo(user)"]

    pg_disconnect $conn

    set conn   
} pgsql3

#
#
#
test pgtcl-1.3 {connect to db using connlist option} {

    set conn [pg::connect -connlist [array get ::conninfo]]

    pg_disconnect $conn

    set conn
} pgsql3

#
#
#
test pgtcl-1.4 {connect to db using -connhandle option} {

    set conn [pg::connect -connlist [array get ::conninfo] -connhandle myhan]

    pg_disconnect $conn

    set conn
} myhan

#
#
#
test pgtcl-2.1 {use connection as command} {

    set conn [pg::connect -connlist [array get ::conninfo] -connhandle myhan]

    set res [myhan exec "SELECT * FROM Pg_class LIMIT 5"]

    pg_result $res -clear

    pg_disconnect $conn

    set res
} myhan.0

#
#
#
test pgtcl-3.1 {test getting connection handles} {

    foreach C [list 1 2 3] {
        set myconn($C) [pg::connect -connlist [array get ::conninfo]]
    }

    set conns [lsort [pg::conninfo]]

    foreach C [list 1 2 3] {
        pg::disconnect $myconn($C)
    }

    set conns
} [lsort [list pgsql3 pgsql4 pgsql5]]

#
#
#
test pgtcl-3.2 {test getting connection handles, after disconnect} {

    foreach C [list 1 2 3] {
        set myconn($C) [pg::connect -connlist [array get ::conninfo]]
    }

    foreach C [list 1 2 3] {
        pg::disconnect $myconn($C)
    }

    pg::conninfo
} [list]

#
#
#
test pgtcl-3.3 {test renaming of connection handle command} {

    set conn [pg::connect -connlist [array get ::conninfo]]

    rename $conn MYCONN
    set res [MYCONN exec "SELECT * FROM Pg_class LIMIT 5"]

    pg_result $res -clear

    pg_disconnect $conn

    set res

} pgsql3.0


#
#
#
test pgtcl-3.4 {test deleting of a command handle} {
    unset -nocomplain res

    set conn [pg::connect -connlist [array get ::conninfo]]

    set res [$conn exec "SELECT * FROM Pg_class  WHERE relname='tables'"]

    pg::result $res -clear
    rename $conn {}

    pg::conninfo
} [list]

#
#
#
test pgtcl-4.1 {get result handles} {

    unset res

    set conn [pg::connect -connlist [array get ::conninfo]]

    foreach R [list 1 2 3 4] {
        set res($R) [$conn exec "SELECT * FROM Pg_class LIMIT $R"]
    }

    set results [lsort [pg::results $conn]]

    foreach R [list 1 2 3 4] {

        pg_result $res($R) -clear
    }

    pg_disconnect $conn

    set results

} [lsort [list pgsql3.0 pgsql3.1 pgsql3.2 pgsql3.3]]

#
##
#
test pgtcl-4.2 {test using result handle command} {

    catch {unset res}

    set conn [pg::connect -connlist [array get ::conninfo]]

    set res [$conn exec "SELECT * FROM Pg_class LIMIT 1"]

    set num [$res -numTuples]
    $res -clear

    pg_disconnect $conn

    set num
} 1

#
#
#
test pgtcl-4.3 {test deleting of a result handle} {
    unset -nocomplain res

    set conn [pg::connect -connlist [array get ::conninfo]]

    set res [$conn exec "SELECT * FROM Pg_class  WHERE relname='tables'"]

    rename $res {}

    #pg::result $res -clear
    pg::results $conn

} [list]

## only check if have dicts installed
if {[llength [info commands dict]] == 1} {
#
#
#
test pgtcl-4.4 {get results as a DICT} {

    unset res

    set conn [pg::connect -connlist [array get ::conninfo]]

    set res [$conn exec "SELECT * FROM Pg_class  WHERE relname='tables'"]

    set results [pg::result $res -dict]

    pg_result $res -clear

    pg_disconnect $conn

    dict get $results 0 relname

} tables
}

#
#
#
test pgtcl-5.1 {quote a string} {

    set qstr [pg::quote "o'reilly"]

    set qstr
} 'o''reilly'

#
#
#
test pgtcl-5.2 {quote a binary string} {

    set bn [binary format a* \000\\HELLO's]
    set qstr [pg::quote_bytea $bn]

    set qstr
} \\\\000\\\\\\\\HELLO\\'s


